@cached
operation EAnnotation getDetail(key : String) : String {
	var detail := self.details.selectOne(d|d.key = key);
	if (detail.isDefined()) {return detail.value;}
	else {return null;}
}

operation EClass getSingleValuedAttributes() {
	return self.eAttributes.select(a|not a.isMany);
}

operation EClass getAllSingleValuedAttributes() {
	return self.eAllAttributes.select(a|not a.isMany);
}

operation EModelElement getAnnotationValue(name : String, detail : String) : Any {
	
	var ann := self.eAnnotations.selectOne(a|a.source = name);
	
	var det;
	
	if (ann.isDefined()) {
		det := ann.details.selectOne(d|d.key = detail);
	}
	
	if (det.isDefined()) {
		return det.value;
	}
	else if (self.isTypeOf(EClass)) {
		for (s in self.eSuperTypes) {
			var sann := s.getAnnotationValue(name, detail);
			if (sann.isDefined()) {
				return sann;
			}
		}
	}
	return det;	
}

operation getConfig(parameter : String) : String {
	return getConfig(parameter, "");
}

operation getConfig(parameter : String, def : Any) : String {
	var ann = EAnnotation.all.selectOne(a|a.source = "config");
	if (ann.isDefined()) {
		var det := ann.details.selectOne(d|d.key = parameter);
		if (det.isDefined()) {
			return det.value;
		}
	}
	return def;
}

@cached
operation EModelElement getAnnotation(name : String) : EAnnotation {
	var ann := self.eAnnotations.selectOne(a|a.source = name);
	
	/*
	if (self.isTypeOf(EClass) and ann.isUndefined()) {
		for (s in self.eSuperTypes) {
			var sann := s.getAnnotation(name);
			if (sann.isDefined()) return sann;
		}
	}*/
	
	return ann;
	
}

@cached
operation EModelElement isAnnotatedAs(name : String) : Boolean {
	return self.getAnnotation(name).isDefined();
}

operation EClass getAllSuperTypes() : Sequence {
	var supers = new Sequence;
	
	for (super in self.eSuperTypes) {
		supers.add(super);
		supers.addAll(super.getAllSuperTypes());
	}
	
	return supers;
}